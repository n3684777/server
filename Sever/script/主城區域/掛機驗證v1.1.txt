//吉米 購買物品型外掛驗證 v1.1版

-	shop	anti_bot_shop	-1,909:-1

-	script	OnNPCKill	-1,{
OnNPCKillEvent:
	@anti_bot_shop = @anti_bot_shop+1;
	if(@anti_bot_shop>=200){goto OnRepeat;}

OnRepeat:
	if(@rand_itemid != 0){end;}
	sc_start SC_FREEZE,-1,0;
	sc_start SC_SILENCE,-1,0;
	sc_start SC_STONE,-1,0;
        @wrong_count = @wrong_count+1;
        deltimer .npc_name$+"::OnJail";
        addtimer ( .duration * 1000 ),.npc_name$+"::OnJail";
        @rand_itemid = .item_list[ rand( .item_list_size ) ];
        @rand_amount = rand( 5,10 );
        dispbottom "[ 驗證系統 ] 請立刻購買 "+@rand_amount+" 個 "+getitemname( @rand_itemid )+"";
        dispbottom "[ 驗證系統 ] 30秒內無動作、關閉購買窗、輸入錯誤3次直接坐牢。";
        npcshopattach .shop_name$,1;
        callshop .shop_name$,1;
        end;
        
OnBuyItem:
        if ( @bought_nameid[0] != @rand_itemid || @bought_quantity[0] != @rand_amount ) {
            @rand_itemid = 0;
            dispbottom "[ 驗證系統 ] 錯誤 "+@wrong_count+" 次";
            if ( @wrong_count < .max_wrong_count ) {
            	sleep2 1000;
                goto OnRepeat;
            }
            else {
                goto OnJail;
            }
        }

	sc_end SC_FREEZE;
	sc_end SC_SILENCE;
	sc_end SC_STONE;
	@anti_bot_shop = rand(-100,-250);
	@wrong_count = 0;
	@rand_itemid = 0;
    	deltimer .npc_name$+"::OnJail";
	dispbottom "[ 驗證系統 ] 感謝配合調查，獲得狀態恢復補償。";
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	sc_start SC_INCREASEAGI,300000,7;
	sc_start SC_BLESSING,300000,7;
	sc_start SC_ASSUMPTIO,300000,7;
        end;
       
OnJail:
	sc_end SC_FREEZE;
	sc_end SC_SILENCE;
	sc_end SC_STONE;
	@wrong_count = 0;
	@rand_itemid = 0;
        atcommand "@jail "+strcharinfo(0);
        end;

OnInit:
        .max_wrong_count = 3;
        .duration = 30;
        .shop_name$ = "anti_bot_shop";
       
        setarray .item_list,
            501,
            502,
            503,
            504,
            505;
        .item_list_size = getarraysize( .item_list );
        .npc_name$ = strnpcinfo(3);
        npcshopdelitem .shop_name$,909;
        for ( .@i = 0; .@i < .item_list_size; .@i++ )
            npcshopadditem .shop_name$,.item_list[.@i],-1;
        end;
}