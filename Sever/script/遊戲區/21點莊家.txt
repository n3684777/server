//===== eAthena Script ======================================= 
//= Black Jack
//===== By: ================================================== 
//= kobra_k88 bug fix 及修改: Momoko@秘密仙境 http://secretro.dyndns.org
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//=
//===== Description: ========================================= 
//= Black Jack card game.  Gameplay based off standard casino
//= black jack rules.  Dealer must have at least 17 to stay and will
//= automatically stay at 17 and up.  Player must have at least
//= 13 to stay.  Aces counted as 11 or 1.  Option to "Double Down".
//= Currently does not allow for "insurance", or "splitting"
//= of pairs.
//===== Additional Comments: ================================= 
//=
//漢化:wack
//感謝jack9685以及激情RO3F玩家提供的資料
//bug fix by Momoko, 修改了在遊戲結束前下線就可以避免扣減賭注的問題
//============================================================ 


cmd_in02,188,89,2	script	21點莊家#1	57,{

	mes "[21點莊家]";
	mes "你好! 想不想來玩一把21點?";
	mes "沉迷賭博，下場等於找忽克連精煉";
	mes "如有沉迷，請電戒賭熱線";
	M_Menu:
	next;
	menu "遊戲規則是怎麼樣的?",M_0, "我要玩.",L_Play, "不，謝了.",M_End;

	M_0:
		mes "[莊家]";
		mes "你想知道有關規則的哪個部分?";
		sM_Menu0:
		next;
		menu "基本規則.",sM_0a, "輸贏的準則.",sM_0b,"有關^FF0000Ace^000000代表的數值.",sM_0d, "取消.",M_Menu;

		sM_0a:
			mes "[21點莊家]";
			mes "21點就是一個紙牌遊戲，這遊戲的目標就是取得21點或者是更接近21點，當然不能超過21點。";
			next;
			mes "[21點莊家]";
			mes "開始遊戲前，玩家必須下注。一旦下注，莊家和玩家每人將發到2張牌。";
			mes "根據你手上的牌你可以選擇 ^5533FF'停牌'^000000,或者^5533FF'要牌'^000000。";
			next;
			mes "[21點莊家]";
			mes "當你選擇'停牌',就是告訴莊家你需要再加任何牌了。";
			mes "然後莊家選擇是否要牌。 ";
			mes "你必須至少達到^FF3355 13點 ^000000才可以停牌。";
			mes "莊家必須要到^FF3355 17點或者17點以上 ^000000才可以停牌。";
			next;
			mes "[21點莊家]";
			mes "當你選擇'要牌',就是告訴莊家你還要加一張牌。";
			mes "你可以通過要牌來增加你的點數.";
			mes "只要你沒超過^FF3355 21 ^000000點，你就一直可以要牌。";
			goto sM_Menu0;
		sM_0b:
			mes "[21點莊家]";
			mes "這裡有幾條勝負規則：";
			next;
			mes "[21點莊家]";
			mes "1.) 在21點最後1回合中：如果你的點數大於莊家的，那麼你就贏了。反之，如果你的點數小於莊家的，那麼你就輸了。";
			next;
			mes "[21點莊家]";
			mes "2.) 如果你要了1張牌而使你的點數超過了21點，那麼你就輸掉這局。這個就叫作^5533FF'爆牌'^000000。";
			mes "如果莊家爆牌，那麼你就贏得了這局。";
			next;
			mes "[21點莊家]";
			mes "3.) 如果你拿的頭2張牌就21點了，那麼你就贏得了這局。";
			mes "當你頭2張拿到1張'Ａ'和1張值為10點的牌,這個就叫作^5533FF'Black Jack'^000000牌型。";
			mes "當莊家拿到'Black Jack',那麼就是莊家贏了。";
			next;
			mes "[21點莊家]";
			mes "4.) 除了勝負，還有就是平局。";
			mes "當你和莊家都是相同的點數時，這種就是沒有勝負沒有輸贏的平局";
			next;
			mes "[21點莊家]";
			mes "這個就叫你和莊家打成^5533FF'平手'^000000。";
			mes "同樣，你和莊家同時拿到'Black Jack'也是這種情況。";
			goto sM_Menu0;
		sM_0c:
			mes "[21點莊家]";
			mes "'加倍'就是把你的下注加倍,但是這樣你就只能再要1張牌。";
			mes "這個只能在每局的第一回合中使用。";
			next;
			mes "[21點莊家]";
			mes "舉1個加倍很有用的例子就是當你一開始的2張牌加起來是11點時。";
			mes "拿1下張牌，你很有可能得到21點或者20點。";
			mes "這個就是加倍的好時機。";
			goto sM_Menu0;
		sM_0d:
			mes "[21點莊家]";
			mes "'Ａ'是在21點中唯一一張特殊的牌，因為它有2個值。";
			mes "'Ａ'可以當作11點也可以當作1點。";
			mes "舉例來說，你拿了1張4和1張A,那麼你得到的點數可以說是15點，也可以說是5點。";
			next;
			mes "[21點莊家]";
			mes "如果你選擇停牌，那麼A將自動被認為是11點，你得到總點數為15點";
			next;
			mes "[21點莊家]";
			mes "如果你選擇要牌然後得到了1張9，那麼A將自動作為1點，你的總點數為14點。";
			mes "如果A作為11點來算，你的點數就超過21點了，將馬上輸掉這個比賽";
			next;
			mes "[21點莊家]";
			mes "因為A在遊戲裡可以有2個不同的值，";
			mes "所以這是1張非常厲害的牌";
			goto sM_Menu0;
	M_End:
		mes "[21點莊家]";
		mes "無論何時都歡迎您的到來";
		close;

//================
L_Play:
	mes "[莊家]";
	mes "請下注吧......";
	next;
	menu "100 Zeny",M_1a, "1000 Zeny",M_1b, "5000 Zeny",M_1c, "10000 Zeny",M_1d, "我是老實人 ....",M_End;

	M_1a:
		if(Zeny < 100) goto sL_NotEnuf;
		set @bet, 100;
		set Zeny, Zeny - @bet;
		goto L_Cont0;
	M_1b:
		if(Zeny < 1000) goto sL_NotEnuf;
		set @bet, 1000;
		set Zeny, Zeny - @bet;
		goto L_Cont0;
	M_1c:
		if(Zeny < 5000) goto sL_NotEnuf;
		set @bet, 5000;
		set Zeny, Zeny - @bet;
		goto L_Cont0;
	M_1d:
		if(Zeny < 10000) goto sL_NotEnuf;
		set @bet, 10000;
		set Zeny, Zeny - @bet;
		goto L_Cont0;

		sL_NotEnuf:
			mes "[莊家]";
			mes "什麼嘛 你沒錢就不要來玩了哦....";
			close;

	L_Cont0:
	mes "(派牌完畢...............)";
	next;
	deletearray $@card[0],13;
	set @dealerTurn, 0;
	set @numP, 0;
	set @numD, 0;
	set @pAce, 0;
	set @dAce, 0;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;

//==============
L_Start:
	callsub sF_GetTot;

	mes "- 這裡是 ^FF5533莊家的^000000 牌:";
	if (@numD==2) callsub sF_D2cards;
	if (@numD==3) callsub sF_D3cards;
	if (@numD==4) callsub sF_D4cards;
	if (@numD==5) callsub sF_D5cards;
	mes "    莊家擁有的總分值為: ^FF5533"+@dealTot+"^000000";
	mes " ";
	mes "- 這裡是 ^5533FF你的^000000 牌:";
	if (@numP==2) callsub sF_P2cards;
	if (@numP==3) callsub sF_P3cards;
	if (@numP==4) callsub sF_P4cards;
	if (@numP==5) callsub sF_P5cards;
	if(@pAce != 1 || @playTot == 21) mes "    你有: ^5533FF" +@playTot+ "^000000";
	if(@pAce == 1 && @playTot != 21) mes "    你有: ^5533FF" +@playTot+ "^000000, 或者 ^5533FF" +(@playTot-10)+ "^000000";
	next;
	if(@playTot==21 && @dealTot==21) goto sL_Push;
	if(@numP==2 && @playTot == 21) goto sL_Win;
	if(@numD==2 && @dealTot == 21) goto sL_Lose;
	if(@playTot > 21) goto sL_Lose;
	if(@dealTot > 21) goto sL_Win;
	if(@numP==2 && @dealerTurn==0) menu "繼續派牌!",M_Hit, "停止派牌",M_Stay, "雙倍下注!",M_Double;
	if(@dealerTurn == 0) menu "繼續派牌!",M_Hit, "停止派牌",M_Stay;

	M_Stay:
		mes "[莊家]";
		if(@playTot < 13) goto sL_PlayToLow;
		if(@dealTot > 16) mes "停止發牌";
		if(@dealTot > 16 || @numD == 5) goto L_Check;
		mes " 莊家選擇繼續派牌 ";
		next;
		callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
		set @dealerTurn, 1;
		goto L_Start;

		sL_PlayToLow:
			mes "你的分值還不夠哦 還是選擇繼續派牌吧.....";
			next;
			goto M_Hit;

	M_Hit:
		if(@numP == 5) goto M_Stay;
		callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
		goto L_Start;
	M_Double:
		mes "[莊家]";
		mes "遊戲者選擇了雙倍下注";
		mes "你現在的下注額為原來的雙倍,而且你現在只可以再抽一張牌了.......";
		next;
		set @dealerTurn, 1;
		set @bet, @bet*2;
		callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
		goto L_Start;
//=============
L_Check:
	next;
	if(@playTot < @dealTot) goto sL_Lose;
	if(@playTot == @dealTot) goto sL_Push;

	sL_Win:
		mes "[莊家]";
		mes "恭喜哦~ 你贏了!  ";
		next;
//		set Casino_tokens, Casino_tokens + @bet*2;
		set Zeny, Zeny + @bet*2;

		goto L_Play;
	sL_Lose:
		mes "[莊家]";
		mes "不好意思~ 你輸了......";
		next;
		goto L_Play;
	sL_Push:
		mes "[莊家]";
		mes "平局. 你與莊家打成平手了~ ";
		next;
		goto L_Play;


//==================================
// Sub function for dealing/pulling the cards
sF_GetCards:
	set @rnd, rand(1,13);
	if($@card[@rnd] == 4) goto sF_GetCards;
	set $@card[@rnd], $@card[@rnd] + 1;
	set getarg(1), @rnd;
	if(getarg(1) > 10) set getarg(1), 10;
	if(getarg(1) == 1 && getarg(3) < 1) set getarg(1), 11;
	if(getarg(1) == 11) set getarg(3), 1;
	set getarg(2), " " + getarg(1) + " ";
	if(@rnd == 10) set getarg(2), getarg(1);
	if(@rnd == 1) set getarg(2), " A ";
	if(@rnd == 11) set getarg(2), " J ";
	if(@rnd == 12) set getarg(2), " Q ";
	if(@rnd == 13) set getarg(2), " K ";
	set getarg(0), getarg(0) + 1;
	return;

//==================================
// Sub function for getting the total score for each hand
sF_GetTot:
	set @i, 0;
	set @dealTot, 0;
	set @playTot, 0;

	GetDealTot:
		set @dealTot, @dealTot + @dealCard[@i];
		set @i, @i + 1;
		if(@i < @numD) goto GetDealTot;
		if(@dAce == 1 && @dealTot > 21) set @dealTot, @dealTot - 10;
		set @i, 0;
	GetPlayTot:
		set @playTot, @playTot + @playCard[@i];
		set @i, @i + 1;
		if(@i < @numP) goto GetPlayTot;
		if(@pAce == 1 && @playTot > 21) set @pAce, 2;
		if(@pAce > 0 && @playTot > 21) set @playTot, @playTot - 10;
	return;

//=======================================
// Sub function for displaying the Cards
sF_D2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_D3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" |   | "+@dealCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_P3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" |   | "+@playCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_D4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@dealCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_P4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" |";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@playCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_D5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[3]+" |   | "+@dealCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[3]+" |   | "+@playCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
}

cmd_in02,168,89,6	duplicate(21點莊家#1)	21點莊家#2	57